plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '1.0.0'
    id "com.diffplug.spotless" version "6.9.1"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("com.azure:azure-storage-blob:12.19.0")
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.0")
    testImplementation("org.mockito:mockito-core:4.9.0")
    testCompile("org.assertj:assertj-core:3.23.1")
}

group = "de.timmhirsens.azureblobcache"
version = "0.1.0"

sourceCompatibility = 1.8

gradlePlugin {
    // Define the plugin
    plugins {
        azureblobcache {
            id = 'de.timmhirsens.azureblobcache.caching'
            displayName = 'Azure Blob Storage Build Cache'
            description = 'This is a Gradle build cache implementation that uses the Azure Blob Storage as a storage backend for cache objects.'
            implementationClass = 'de.timmhirsens.azureblobcache.AzureBlobBuildCachePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/fr1zle/azureblob-build-cache'
    vcsUrl = 'https://github.com/fr1zle/azureblob-build-cache.git'
    tags = ['build-cache', 'azure']
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "fr1zle_azureblob-build-cache"
    }
}
